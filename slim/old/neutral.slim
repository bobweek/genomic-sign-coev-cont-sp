// Keywords: continuous space, continuous spatial landscape, reprising boundaries, spatial competition, fitness()

initialize() {
	initializeSLiMOptions(dimensionality="xy",periodicity="xy");
	initializeTreeSeq();
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
	
	// Set up an interaction for spatial competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=0.3);
	i1.setInteractionFunction("n", 3.0, 0.1);
}
1 late() {
	sim.addSubpop("p1", 500);
	
	// initial positions are random in ([0,1], [0,1])
	p1.individuals.x = runif(p1.individualCount);
	p1.individuals.y = runif(p1.individualCount);
}
1: late() {
	// evaluate interactions before fitness calculations
	i1.evaluate();
}
fitness(NULL) {
	// spatial competition
	totalStrength = i1.totalOfNeighborStrengths(individual);
	return 1.1 - totalStrength / p1.individualCount;
}
modifyChild() {
	// draw a child position near the first parent, accounting for periodic boundaries
	pos = parent1.spatialPosition + rnorm(2, 0, 0.02);
	//child.y = parent1.y + rnorm(1, 0, 0.02);
	if (p1.pointInBounds(pos)) {				
		child.setSpatialPosition(pos);
	}else child.setSpatialPosition(p1.pointPeriodic(pos)); // enforce periodic boundaries
	return T;
}
5000 late() { 
	sim.outputFixedMutations();
	sim.treeSeqOutput("/home/bb/gits/genomic-sign-coev-cont-sp/slim/neut_sim.trees");
	catn("Done.");
   sim.simulationFinished();
 }
