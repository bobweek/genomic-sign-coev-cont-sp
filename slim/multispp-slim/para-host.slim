species host initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy",periodicity="xy"); // change to absorbing
	initializeSpecies(tickModulo=1, tickPhase=1, avatar="ðŸ¦Œ");
	initializeTreeSeq();
	
	// model parameters
	defaults = Dictionary(
		"Lh", 500000,
		"Lp", 500000,
		"hFX", 0.5,  			// host mutation effect size
		"pFX", 0.5,  			// parasite mutation effect size
		"hSD", 2.0,  			// host dispersal distance	
		"pSD", 2.0,  			// parasite dispersal distance
		"SI", 1.0,  			// spatial interspp interaction distance
		"W", 100.0,  			// width and height of the simulated area
		"ch", 0.02, 			// strength of host competition
		"cp", 0.02, 			// strength of para competition	
		"lmbda", 2.0,			// (spatial) niche breadth
		"h_base", 1.1,		// host base fitness
		"p_base", 1.1,		// para base fitness
		"hc", 0.98, 		// multiplicative cost on host fitness for each infection
		"pc", 0.99, 		// multiplicative cost on parasite fitness for failing to infect
		"pb", 1.01, 		// multiplicative benefit on parasite fitness for successful infection
		"minpr", 0.2,		// minimum probability of infection (when genotypes don't match)
		"maxpr", 1.0,		// maximum probability of infection (when genotypes do match)
		"Î³", 0.1				// sensitivity of inf pr to trait differences
		);
	for (k in defaults.allKeys) {
		if (!exists(k)) {
			defineConstant(k, defaults.getValue(k));
		} else {
			defaults.setValue(k, executeLambda(paste(c(k, ";"), sep='')));
		}
	}
	defineGlobal("params", defaults);
	defineConstant("hSM", hSD);  			// host mate choice distance
	defineConstant("pSM", pSD);  			// parasite mate choice distance

	
	// host genomics
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "n", 0.0, hFX);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 1e8-1);
	initializeRecombinationRate(1e-8);
	
	// host spatial competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=lmbda);
	
	// host mate choice
	initializeInteractionType(3, "xy", reciprocal=T, maxDistance=hSM);
	
	// parasitism
	initializeInteractionType(5, "xy", reciprocal=T, maxDistance=SI);
}

species para initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy",periodicity="xy");
	initializeSpecies(tickModulo=1, tickPhase=1, avatar="ðŸª±");
	initializeTreeSeq();
	
	// parasite genomics
	initializeMutationRate(1e-7);
	initializeMutationType("m2", 0.5, "n", 0.0, pFX);
	initializeGenomicElementType("g2", m2, 1.0);
	initializeGenomicElement(g2, 0, 1e8-1);
	initializeRecombinationRate(1e-8);
	
	// parasite spatial competition
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=lmbda);
	
	// parasite mate choice
	initializeInteractionType(4, "xy", reciprocal=T, maxDistance=pSM);	
}

// make causal mutions neutral :B
species host fitness(m1) { return 1.0; }
species para fitness(m2) { return 1.0; }

// host mating
species host reproduction(p1) {
	mate = i3.drawByStrength(individual, 1); // find a single partner to mate with
	if(mate.size()) {
		nOff = rpois(1, individual.tagF);
		for(i in seqLen(nOff)) {
			pos = individual.spatialPosition + rnorm(2, 0, hSD); // add dispersal
			bb = subpop.addCrossed(individual, mate);			
			bb.setSpatialPosition(p1.pointPeriodic(pos));
		}
	}
	return;
}

// parasite mating
species para reproduction(p2) {
	mate = i4.drawByStrength(individual, 1);
	if(mate.size()) {
		nOff = rpois(1, individual.tagF);
		for(i in seqLen(nOff)) {
			pos = individual.spatialPosition + rnorm(2, 0, pSD); // add dispersal
			bb = subpop.addCrossed(individual, mate);			
			bb.setSpatialPosition(p2.pointPeriodic(pos));
		}
	}
	return;
}

ticks all 1 early() {
	IPS = asInteger(1 * W * W); // Initial Population Size
	host.addSubpop("p1", IPS);
	para.addSubpop("p2", IPS);
	p1.setSpatialBounds(c(0, 0, W, W));
	p2.setSpatialBounds(c(0, 0, W, W));
	p1.individuals.setSpatialPosition(p1.pointUniform(IPS));
	p2.individuals.setSpatialPosition(p1.pointUniform(IPS));
}

ticks all late(){
	i1.evaluate(p1);
	i2.evaluate(p2);
	i3.evaluate(p1);	
	i4.evaluate(p2);
	i5.evaluate(p1);
	
	// caculating the phenotype as sum of loci with mutation
	h_trt = p1.individuals.sumOfMutationsOfType(m1);
	p_trt = p2.individuals.sumOfMutationsOfType(m2);
	
	// save trait values
	p1.individuals.z = h_trt;
	p2.individuals.z = p_trt;
	
	// identify genotypes by brightness
	for(ind in p1.individuals){
		val = (ind.z%100)/100;
		ind.color = rgb2color(hsv2rgb(c(val, 0.75, 0.75)));
	}
	for(ind in p2.individuals){
		val = (ind.z%100)/100;
		ind.color = rgb2color(hsv2rgb(c(val, 0.75, 0.75)));
	}
	
	// compute fx of intraspp comp on host fecun
	for(h in p1.individuals){
		h_loc = c(h.x,h.y);
		h_nbs = i1.interactingNeighborCount(h);
		h.tagF = h_base*exp(-ch*h_nbs);
	}
			
	// compute fx of intraspp comp on para fecun
	// and incorporate fx of interspp intxns on host/para fecun
	for(p in p2.individuals){
		p_loc = c(p.x,p.y);
		p_nbs = i2.interactingNeighborCount(p);
		p_fit = p_base*exp(-cp*p_nbs);
		
		// if there's any hosts out there
		if( p1.individualCount > 0 ){
			// draw hosts within radius SI of focal parasite
			h_nbrs= i5.nearestNeighborsOfPoint(p1, p_loc, count=p1.individualCount);
		} else h_nbrs = c();	
		
		// if there's hosts nearby
		if(length(h_nbrs) > 0){
			
			// choose one living host at random for parasite to interact with
			hnb = h_nbrs[rdunif(1, 0, length(h_nbrs)-1)];
			
			// trait-matching-model
			inf_pr = maxpr*exp(-Î³*(h_trt[hnb.index]-p_trt[p.index])^2);
			
			// deterministic fitness outcome is faster and easier to analyze
			p_fit = (pb*inf_pr+pc*(1-inf_pr))*p_fit;
			hnb.tagF = (hc*inf_pr+(1-inf_pr))*hnb.tagF;
			
		}else p_fit = pc*p_fit;	// cost of not having anyone to infect
		// note: each parasite infects at most one host, but each host can be infected by multiple parasites
		//			hence, if a host is infected by three parasites it will incur the fitness cost of hc^3
		p.tagF = p_fit;
	}
	
	// forces non-overlapping generations	
	p2.individuals.fitnessScaling = 0;
	p1.individuals.fitnessScaling = 0;
}

ticks all 2000 late() {
	host.treeSeqOutput("host.trees", metadata=params);
	para.treeSeqOutput("para.trees", metadata=params);
	catn("Done.");
   host.simulationFinished();
   para.simulationFinished();
}